// Borg model - task

task := {{ 
    tasknum:0, start_date:time, due_date:time, person_assigned, priority:0, state, type, 
    description, resolution, category, close_date:time, project:0, summary, 
    create_time:time, lastmod:time, uid, url 
}}

// Find next id
getnext_task() => do {
    task [ { fold(max,tasknum) } ] +1
}

//--- Task REST CRUD

// Add a task
add_task(cl:task) => do {
    task := union cl [ { *
        tasknum := getnext_task(),
        create_time := now(), 
        lastmod := now() 
    } ]
}

// Replace a task
put_task_id(id:0, cl:task) => do {
    task[ ?(tasknum = id) ]
    task := union cl [ { *
        tasknum := id,
        create_time := now(), 
        lastmod := now() 
    }]
}

// Delete task by id
delete_task_id(id) => do {
    task := [ ?(tasknum = id) ]
}

// Find task by id
get_task_id(id) => do {
    task[ ?(tasknum = id) ]
}

// Find all tasks
get_task() => do {
    task
}

//--- Task other queries

// Find all ids
get_task_ids() => do {
    task [ { tasknum} ]
}

// Rename task type
put_task_type(t1, t2) => do {
    task := [ ?(type = t1) { * type := t2 } ]
}

