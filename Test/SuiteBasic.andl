// Test Suite 1 -- basic testing of language features

#catalog

start($filename$)

// Literals
assert($lineno$, '"' "Hel""lo" h'20' 'World' '!' h'0022' = '"Hello World!"')
assert($lineno$, "Hello" & d'32' & "World" = 'Hello World')
assert($lineno$, "Number: " & (17+22) & " Date: " & t'31/12/2015' & " Bool: " & (6=6) = "Number: 39 Date: 31/12/2015 Bool: true")
assert($lineno$, "01/01/2015" < "31/12/2014")
assert($lineno$, t'01/01/2015' > t'31/12/2014')
i':=' "''" d'32' := b'2a2b2cd1e2f3'
assert($lineno$, i':=' h'27 0027 20'  = b'2A2B2CD1E2F3')

assert($lineno$, {{}} = {{}})
assert($lineno$, {{:}} = {{:}})
assert($lineno$, {{:}()} = {{:}()})
assert($lineno$, {{:}()} = {{}})
assert($lineno$, {{}} <> {{:}})

assert($lineno$, count({{:}}) = 0)
assert($lineno$, count({{:}()}) = 1)
assert($lineno$, count({{}}) = 1)
assert($lineno$, {{}} .count = 1)

assert($lineno$, false = (true and false))
assert($lineno$, true = (true or false))
assert($lineno$, true = (true xor false))

assert($lineno$, 3 = (7 and 11))
assert($lineno$, 15 = (7 or 11))
assert($lineno$, 12 = (7 xor 11))

//--- if(,,)
assert($lineno$, if(true,"yes",'no') = 'yes')
assert($lineno$, if(false,0,1+2) = 3)

//--- assign
a1 := 22
assert($lineno$, a1 = 22)
a2 := a1*2
assert($lineno$, a2 = 44)


//--- builtin
assert($lineno$, seq(3) = {{N:=0},{N:=1},{N:=2}})
assert($lineno$, seq(3) [{nnn:=N}] = {{nnn:=0},{nnn:=1},{nnn:=2}})

//--- system type 'date' --- defer for now
tim1 := t'2015/02/26'
//dat1 := date(tim1)
//dat2 := dateymd(2015,2,27)
//dat1 = date(tim1)
//dat1 < dat2
//dat1.year = 2015
//dat1.month = 2
//dat1.day = 26
//dat2.dow = 5 // Friday
//daysdiff(dat2,dat1) = 1

//--- string operations
ss1 := 'abcdefg'
ss2 := 'XYZ   '
assert($lineno$, ss2.trim = 'XYZ')
assert($lineno$, left(ss1,10) = 'abcdefg   ')
assert($lineno$, left(ss1,3) = 'abc')
assert($lineno$, left(ss1,0) = '')
assert($lineno$, right(ss1,3) = 'efg')
assert($lineno$, right(ss1,10) = '   abcdefg')
assert($lineno$, before(ss1,'cd') = 'ab')
assert($lineno$, after(ss1,'cd') = 'efg')
assert($lineno$, ss1.toupper = 'ABCDEFG')
assert($lineno$, ss2.tolower = 'xyz   ')
assert($lineno$, fill('abc', 20) = 'abcabcabcabcabcabcab')
assert($lineno$, fill('abc', 0) = '')
assert($lineno$, fill('abc', -20) = '')
assert($lineno$, ss1.length = 7)

//--- user type defs
def utype1(age:0, name:'')
uvar1 := utype1(17, 'smith')
assert($lineno$, uvar1 = utype1(17, 'smith'))
assert($lineno$, uvar1.age = 17)
assert($lineno$, uvar1.name = 'smith')
uvar11 := utype1(18, 'jones')
assert($lineno$, uvar11 > uvar1)

def utype2(age:0, name:'', given:'')
uvar2 := utype2(63, 'jones', 'mary')
assert($lineno$, uvar2 = utype2(63, 'jones', 'mary'))
assert($lineno$, uvar2.age = 63)
assert($lineno$, uvar2.name = 'jones')
assert($lineno$, uvar2.given = 'mary')

finish($filename$)
