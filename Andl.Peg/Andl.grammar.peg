// Andl grammar - Pegasus
// See: https://github.com/otac0n/Pegasus/wiki/Syntax-Guide

@namespace Andl.Peg
@classname PegParser
@using System.Linq

// start rule: Init for error handling, Start to load catalog
MainStart <AstStatement> = &{ PrintLine(state) } &{ SetState(state) } DirectiveOrBlank* &{ Start() } v:MainNext { v }
    / MainRestart;  // suppress warning -- never taken

// Continue after error
MainRestart <AstStatement> =  Skip v:MainNext { v }

// Parse just one blank line, directive, statement or block
MainNext <AstStatement> =  v:MainStatement &{ SetState(state) } { v };
MainStatement <AstStatement> = WSO EOFX { null }
    / DirectiveOrBlank v:MainStatement { v }
    / WSO v:Statement WSO (EOFX / EOLchk EOLX) { v };

// A do body is just {} or { statements }, EOF not allowed
DoBody <AstValue> = WSO LCexp &{ AST(state).Enter() } WSC v:DoBodyLine* &{ AST(state).Exit() } WSO RCexp { AST(state).DoBlock(v) };
DoBodyLine <AstStatement> = DirectiveOrBlank { AST(state).Empty() } // Note: DoBlock must discard these
    / WSO !RC v:Statement WSO (&RC / EOLchk EOLX) { v };

// directives
DirectiveOrBlank = BlankLine / DirectiveLine;
//DirectiveOrBlank <AstStatement> = (BlankLine / DirectiveLine)  { AST(state).Empty() };
BlankLine = WSO &EOL EOLX;
DirectiveLine = Directive WSO EOLchk EOLX;      // take directive and end of line

Directive = "#" &{ PrintLine(state) } (CatalogDirective / IncludeDirective / NoisyDirective / StopDirective / PanicDirective / BadDirective);
CatalogDirective = "catalog" IDX v:CatalogOption* (&EOL / &{ Error(state, "option expected") } ) { CatalogDirective(state, v) };
CatalogOption = v:("new" / "load" / "update") IDX { v };
IncludeDirective = "include" IDX v:(!CC .)* { IncludeDirective(state, String.Concat(v).Trim()) };
NoisyDirective = "noisy" IDX v:Integer { NoisyDirective(state, String.Concat(v)) };
StopDirective = "stop" IDX v:Integer? { StopDirective(state, String.Concat(v)) };
PanicDirective = "panic" IDX #ERROR{ "panic" };
BadDirective = &{ Error(state, "bad directive") };

//--- statements
Statement <AstStatement> = v:(Deferred 
    / Typedef 
    / Import 
    / Update
    / Assignment 
    / Expression 
    / BadStatement) { v };

BadStatement <AstStatement> = WSO RC &{ Error(state, "mismatched right brace") }  { null }
    / . &{ Error(state, "bad statement") }  { null };

Typedef <AstDefine> = DEF v:(UserType / Subtype)<0,,SEP> { AST(state).DefBlock(v) };
UserType <AstDefine> = v:DefGlobalExp LP 
    &{ AST(state).Enter() } w:FieldList &{ AST(state).Exit() } RPexp 
    { AST(state).UserType(v, w.ToArray()) } ;
Subtype <AstDefine> = v:DefGlobalExp COLON w:TypeRef 
    { AST(state).SubType(v, w) };

Import <AstBlock> = VAR v:ImportSource<0,,SEP> { AST(state).Block(v) };
ImportSource <AstStatement> = v:DefGlobalExp LP w:SourceName RPexp { AST(state).Source(v, AST(state).Text(w)) };
SourceName = v:Ident  &{ IsSourceName(v) } { v }
    / &RP { "" }
    / &{ Error(state, "source name expected") } { "" };

Update <AstStatement> = 
      UPDATE v:CatVar w:Funcop x:Funcop? y:ExpressionExp { AST(state).UpdateSetop(v,w,Single(x),y) }
    / UPDATE v:CatVar &{ AST(state).Enter(v) } 
             w:Where? x:TransformOrDelete &{ AST(state).Exit(true) } { AST(state).UpdateWhere(v,Single(w),x) }
    / UPDATE &{ Error(state, "bad update") }  { null };

TransformOrDelete <AstTransformer> = x:Transformer { x }
    / DOT DELETE { null }
    / . &{ Error(state, "missing update action") }  { null };

CatVar <AstValue> = v:CatIdent { AST(state).Variable(v) };

// Assignment -- does its own type check
Assignment <AstStatement> = &(Ident LA) v:Ident LA w:Expression { AST(state).Assignment(v,w) }
    / Ident LA &{ Error(state, "expression expected") } { null };

// Deferred requires a field list either empty or ident colon
// otherwise backtrack to function call
Deferred <AstStatement> = v:Ident 
    w:(<AstType> COLON a:TypeRef { a } / { null }) 
    x:(<IList<AstField>> LP &(RP RA / Ident COLON) b:FieldList RP { b })? RA 
    &{ AST(state).Enter(v, w, Single(x)) } y:DeferredBody &{ AST(state).Exit(true) } { AST(state).Deferred(v,w,Single(x),y) };

DeferredBody <AstBodyStatement> = v:Statement { AST(state).BodyStatement(v) }
    / BadStatement { null };

//----- expressions
OpenAttrExprExp = (OpenAttrExpr 
    / &{ Error(state, "invalid attribute expression") } { null });
ExpressionExp = Expression
    / &{ Error(state, "expression expected") } { null };

OpenAttrExpr = OpenExpression; // may have a fold
OpenExpression = Expression;  // needs to do name lookup
ExpressionList = v:Expression<0,,SEP>;

Expression <AstValue> = v:Primary w:BinopTail* { AST(state).Binop(v,w) } 
BinopTail <AstOpCall> = v:Binop WSC w:Primary { AST(state).OpCall(v,w) } 

Primary <AstValue> = "-" v:Primary { AST(state).Function("u-",v) }
    / v:Unop w:Primary { AST(state).Function(v,w) }
    / v:SimplePrimary &{ AST(state).Enter(v) } w:PostFixOp* &{ AST(state).Exit(true) } { AST(state).PostFix(v,w) };

//--- post fix operators
PostFixOp <AstCall> = &(WSL DOT) WSC v:PostFixOpBody { v }
    / DOT &{ Error(state, "component or function expected") } { null };

PostFixOpBody <AstCall> = v:Recurser { v }
    / v:Transformer { v }
    / v:Where { v }
    / v:Orderer { v }
    / DOT v:Funcop { AST(state).DotFunc(v) }
    / DOT v:Component { AST(state).DotComponent(v) }
    / DOT v:FieldIdent { AST(state).DotField(v) }

//--- Tuple generators

Recurser <AstOpCall> = DOT WHILE LPexp v:ExpressionExp RPexp { AST(state).While(v) }

Where <AstWhere> = DOT WHERE LPexp v:OpenExpression RPexp { AST(state).Where(v) };

Orderer <AstOrderer> = DOT ORDER LPexp v:OrderTermList RPexp { AST(state).Orderer(v) };
OrderTermList = OrderTerm<0,,SEP>;
OrderTerm <AstOrderField> = w:PERCENT? v:MINUS? x:FieldIdent { AST(state).OrderField(x, v.Count > 0, w.Count > 0) };

Transformer <AstTransformer> = DOT LC WSC v:STAR? WSC w:TransformList SEP? RCexp { AST(state).Transformer(v.Count > 0, w) }
TransformList = Transform<0,,SEP>;
Transform <AstField> = !RC v:FieldIdent &(SEP / RC) { AST(state).Transfield(v) }        // project
    / !RC v:Ident LA WSC w:FieldIdent &(SEP / RC) { AST(state).Transfield(v, w) }       // rename
    / !RC v:Ident LA WSC w:OpenAttrExprExp { AST(state).Transfield(v, null, w) }        // extend
    / !RC v:OpenAttrExprExp { AST(state).Transfield(null, null, v) };                   // lift

//----- Primaries
SimplePrimary = DoBlock 
    / LP v:Expression RPexp { v }
    / Table / Row
    / Fold / If
    / Function
    / v:Literal { v }
    / v:VarIdent  { AST(state).Variable(v) }
    / v:Ident &{ Error(state, "undefined '{0}'", v) } { null };

If <AstValue> = IF LPexp v:Expression SEPexp w:Expression SEPexp x:Expression RPexp { AST(state).If(v,w,x) }
    / IF LP  &{ Error(state, "expression expected") } { null };

Fold <AstValue> = FOLD LPexp v:Foldableop SEPexp w:Expression RPexp { AST(state).Fold(v,w) }
    / FOLD LP  &{ Error(state, "fold operator expected") } { null };

DoBlock <AstValue> = DO v:DoBody { v }

Function <AstValue> = v:Funcop LP w:ExpressionList RPexp { AST(state).Function(v, w.ToArray()) };

Table <AstValue> = &(LC WSL LC) &{ AST(state).Enter() } LC WSC v:TableBody RCexp &{ AST(state).Exit() } { v }
    / REL LP &{ AST(state).Enter() } v:TableBody RPexp &{ AST(state).Exit() } { v }
    / (REL / LC WSC LC) &{ Error(state, "invalid table body") } { null };

TableBody <AstValue> = LC STAR RCexp  { AST(state).Table() }
    / v:TableHeading WSC w:RowValueList SEP? { AST(state).Table(v,w) }
    / v:RowList SEP? { AST(state).Table(v) };

TableHeading <AstValue> = LC COLON RC { AST(state).Heading(null) }
    / LC &(Ident COLON) v:FieldList RCexp { AST(state).Heading(v) };

RowValueList = v:RowValue<0,,SEP>;
RowValue <AstValue> = LP v:ExpressionList SEP? RPexp { AST(state).RowValues(v) };

RowList = v:Row<0,,SEP>;
Row <AstValue> = LC STAR RCexp { AST(state).Row() }
    / LC v:RowTransList RCexp { AST(state).Row(AST(state).Transformer(v)) }
    / TUP LPexp v:Expression RPexp { AST(state).Row(v) };
//    / v:VarIdent  { AST(state).Variable(v) };

RowTransList = RowTrans<0,,SEP>;
RowTrans <AstField> = v:FieldIdent &(SEP / RC) { AST(state).Transfield(v) }
    / v:Ident LA w:Expression { AST(state).Transfield(v, null, w) }

//--- type reference
TypeRef <AstType> = v:Typename &{ IsTypename(v) } { AST(state).FindType(v) }
    / TUP COLON v:Heading { AST(state).TupType(v) }
    / REL COLON v:Heading { AST(state).RelType(v) }
    / v:SimplePrimary { AST(state).GetType(v) }
    / &{ Error(state, "type expected") } { null };

Heading <AstValue> = LC COLON RC { AST(state).Heading(null) }
    / LC &(Ident COLON) v:FieldList RCexp { AST(state).Heading(v) };

FieldList = v:FieldTerm<0,,SEP>;
FieldTerm <AstField> = v:DefLocal COLON w:TypeRef { AST(state).FieldTerm(v, w) }
    / v:DefLocalExp { AST(state).FieldTerm(v, null) };

//--- expecteds
DefGlobalExp = DefGlobal 
    / v:Ident &{ Error(state, "previously defined '{0}'", v) } { null }
DefLocalExp = DefLocal 
    / v:Ident &{ Error(state, "already defined in this scope '{0}'", v) } { null }
LCexp = (WSC LC / &{ Error(state, "left brace expected") } );
RCexp = (WSC RC / &{ Error(state, "right brace expected") } );
LPexp = (WSC LP / &{ Error(state, "left parenthesis expected") } );
RPexp = (WSC RP / &{ Error(state, "right parenthesis expected") } );
SEPexp = (WSC SEP / &{ Error(state, "comma expected") } );
EOLchk = (&EOL / &{ Error(state, "end of line expected") } );

//----- terminals
Typename = v:Ident &{ IsTypename(v) } { v };
Foldableop = v:Funcop &{ IsFoldableop(v) } { v };
Binop = v:Funcop &{ IsBinop(v) } { v };
Unop = v:Funcop &{ IsUnop(v) } { v };
Funcop = v:(Ident / Operator) &{ IsFuncop(v) } { v };
Component = v:Ident &{ IsComponent(v) } { v };
FieldIdent = v:Ident &{ IsField(v) } { v };
VarIdent = v:Ident &{ IsVariable(v) } { v };
CatIdent = v:Ident &{ IsCatVar(v) } { v };
DefGlobal = v:Ident &{ CanDefGlobal(v) } { v };
DefLocal = v:Ident &{ CanDefLocal(v) } { v };

//--- terminals -- each swallows following whitespace
Ident = !KEYWORD v:IdentName WSO { v };
IdentName = v:IdentQuoted WSO w:TextString { v + w }
    / IdentQuoted 
    / IdentString;
Operator = v:OpSym WSO { v };
Literal<AstValue> = v:Literals WSO { v };
Literals<AstValue> = TRUE { AST(state).Bool("true") }
    / FALSE { AST(state).Bool("false") }
    / v:BinaryQuoted { AST(state).Binary(v) }
    / v:TimeQuoted { AST(state).Time(v) }
    / v:NumberString { AST(state).Number(v) }
    / v:IntegerString { AST(state).Number(v) }
    / v:TextString { AST(state).Text(v) };
Integer = IntegerString WSO;

KEYWORD = DEF / DO / VAR / TUP / REL / TRUE / FALSE;

// note: tokens followed by WSC will swallow an EOL
DO = "do" IDX;
DEF = "def" IDX;
REL = ("rel" / "relation") IDX;
TUP = ("tup" / "tuple") IDX;
VAR = "var" IDX;

IF = "if" IDX;
DELETE = "delete" IDX;
FOLD = "fold" IDX;
ORDER = "order" IDX;
UPDATE = "update" IDX;
WHERE = "where" IDX;
WHILE = "while" IDX;

TRUE = "true" IDX;
FALSE = "false" IDX;

RA = "=>" WSO;
LA = ":=" WSO;
COLON = ':' !OpChar WSO;
SEP = ',' WSC;
DOT = '.' WSO;
LC = '{' WSO;
RC = '}' WSO;   // note: gets its own handling
LP = '(' WSC;
RP = ')' WSO;
//LB = '[' WSO;
//RB = ']' WSO;
STAR = '*' !OpChar WSO;
PERCENT = '%' WSO;
MINUS = '-' !OpChar WSO;

IDX = !ID WS*;              // end of ident possibly followed by WS

Skip = (!EOL .)* (EOFX / EOLX);       // skip the rest of the line and hope it then makes sense
WSL = WSO (EOL WSO)*;       // suitable for lookahead
WSC = WSO (EOLX WSO)*;      // continuation, take eol if found, plus WS
WSO = WS*;                  // optional whitespace between tokens
WS = (SP / Comment) { "" };
SP = [ \t];
Comment = "//" (!EOL .)*
        / "/*" (!"*/" (EOLX / .))* "*/";

EOLX = &EOL &{ PrintLine(state) } EOL; // consumed EOL, so print it
EOFX = &EOF &{ PrintLine(state) } EOF;
EOL = "\r"? "\n" { "" };
EOF = !.;

// More complex lexical tokens, but not for use as terminals
//TextString = v:(SingleQuoted / DoubleQuoted / CharHexQuoted / CharDecQuoted)+ { String.Concat(v) };

TextString = v:TextQuoted<1,,WSO> { String.Concat(v) };
TextQuoted = SingleQuoted / DoubleQuoted / CharHexQuoted / CharDecQuoted;

// Lexical tokens, not including white space
SingleQuoted = SQ v:("" (!(CC / SQ) . )* ) SQ { v };
DoubleQuoted = DQ v:("" (!(CC / DQ) . )* ) DQ { v };
CharHexQuoted = "h" SQ SP* v:HexDigits<1,,SP+> SP* SQ { NumToStr(v.ToArray(),16) };
//CharHexQuoted = "h" SQ v:("" SP* (HexDigit+ SP*)*) SQ { v };
CharDecQuoted = "d" SQ SP* v:Digits<1,,SP+> SP* SQ { NumToStr(v.ToArray(),10) };
BinaryQuoted  = "b" SQ v:("" HexDigit*) SQ { v };
TimeQuoted    = 't' SQ v:("" (Digit / Letter / TimeChar)+) SQ { v };
NumberString  = '.' Digit+ / Digit+ ('.' Digit*)?;
IntegerString = "" Digit+ / "$" Digit HexDigit*;
IdentQuoted   = "i" SQ v:("" (!CC !SQ .)* ) SQ { v };
IdentString = (Letter / IdChar) (Letter / Digit / IdChar / IdCharX)*;
OpSym = OpChar OpChar?;
ID = Letter / Digit / IdChar / IdCharX;
Digits = v:Digit+ { String.Concat(v) };
HexDigits = v:HexDigit+ { String.Concat(v) };

// character sets (not tokens)
Letter = [a-zA-Z];
Digit = [0-9];
HexDigit = [a-fA-F0-9];
OpChar = [&=<>:~*/+-];
IdChar = [_$@^];
IdCharX = [#%&?!~`];
TimeChar = [: ./-];
CC = [\x00-\x1f];
SQ = "'";
DQ = '"';
