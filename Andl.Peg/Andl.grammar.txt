// Andl grammar - Pegasus
// See: https://github.com/otac0n/Pegasus/wiki/Syntax-Guide

@namespace Andl.Peg
@classname PegParser
@using System.Linq


// start rule
MainStart <AstStatement> = Init v:MainRestart { v };
MainRestart <AstStatement> = WSE* v:MainStatement { AST(state).Node(v) };
MainStatement <AstStatement> = WSO EOF { null }
    / v:Statement EOLexp { v };

DoBody <AstValue> = LC RC { AST(state).Block(null) }
    / LCexp WSC v:DoBodyLine+ WSO RCexp { AST(state).Block(v) };
DoBodyLine <AstStatement> = WSO !RC v:Statement (&RC / EOLexp WSE+) { v };

// directives
Directive = "#" (CatalogDirective / IncludeDirective / NoisyDirective / StopDirective / PanicDirective / BadDirective);
CatalogDirective = "catalog" IDX v:CatalogOption* (&EOL / #STATE{ ParseError(state, "option expected"); } Skip ) { CatalogDirective(v) };
CatalogOption = v:("new" / "load" / "update") IDX { v };
IncludeDirective = "include" IDX v:(!CC .)* { IncludeDirective(String.Concat(v).Trim()) };
NoisyDirective = "noisy" IDX v:Integer { NoisyDirective(String.Concat(v)) };
StopDirective = "stop" IDX v:Integer? { StopDirective(String.Concat(v)) };
PanicDirective = "panic" IDX #ERROR{ "panic" };
BadDirective = #STATE{ ParseError(state, "bad directive");} Skip;

// statements
Statement <AstStatement> = WSO &. v:(<AstStatement> Deferred / Typedef / Import / UpdateJoin / UpdateTransform / Assignment / Expression / BadStatement) { v };

Typedef <AstDefine> = DEF v:(UserType / Subtype) { v };
UserType <AstDefine> = v:DefinableExp LP w:FieldList RPexp { AST(state).UserType(v, w.ToArray()) } ;
Subtype <AstDefine> = v:DefinableExp COLON w:Type { AST(state).SubType(v, w) };
Type <AstType> = v:Typename &{ IsTypename(v) } { AST(state).FindType(v) }
    / v:SimplePrimary { AST(state).GetType(v) }
    / #STATE{ ParseError(state, "type expected"); } { null };

Import <AstBlock> = DB v:ImportSource<0,,SEP> { AST(state).Block(v) };
ImportSource <AstStatement> = v:DefinableExp LP w:SourceName RPexp { AST(state).Source(v, AST(state).Text(w)) };
SourceName = v:Ident  &{ IsSourceName(v) } { v }
    / &RP { "" }
    / #STATE{ ParseError(state, "source name expected"); } { "" };

Assignment <AstStatement> = &(Ident LA) v:DefinableExp LA w:OpenExpression { AST(state).Assignment(v,w) }
    / Definable LA #STATE{ ParseError(state, "expression expected"); } { null };

// must be able to backtrack from RA to pick up function call
Deferred <AstStatement> = v:Definable 
    w:(<AstType> COLON a:Type { a } / { null }) 
    x:(<IList<AstField>> LP b:FieldList RP { b })? RA 
    &{ DefScope(v, w, Single(x)) } y:DeferredBody &{ PopScope() } { AST(state).Deferred(v,w,Single(x),y) };

DeferredBody <AstStatement> = 
    (UpdateJoin 
    / UpdateTransform 
    / OpenAttrExpression 
    / BadStatement);

// prefer: &{ w.IsJoinOp() } 
UpdateJoin <AstStatement> = 
    v:CatVar LA &("union" / "minus") w:Funcop x:ExpressionExp
    { AST(state).UpdateJoin(v,w,x) };
UpdateTransform <AstStatement> = 
    v:CatVar LA &LB &{ PushScope(AST(state).Variable(v)) } w:TransformTailExp &{ PopScope() } 
    { AST(state).UpdateTransform(v,w) };
BadStatement <AstStatement> = #STATE{ ParseError(state, "bad statement");} Skip { null };

TransformTailExp = TransformTail
    / #STATE{ ParseError(state, "transform arguments expected"); } { null };
TransformTail <AstTranCall> = LB v:Where? WSC w:Orderer? WSC x:Transformer? RBexp { AST(state).TransformTail(Single(v), Single(w), Single(x)) };

Where <AstValue> = QUERY LPexp v:LookupExpression RPexp { v };

Orderer <AstOrderer> = DOLLAR LPexp v:OrderTermList RPexp { AST(state).Orderer(v) };
OrderTermList = OrderTerm<0,,SEP>;
OrderTerm <AstOrder> = v:MINUS? w: PERCENT? x:FieldIdent { AST(state).Order(x, v.Count > 0, w.Count > 0) };

Transformer <AstTransformer> = LC WSC v:STAR? WSC w:TransformList RCexp { AST(state).Transformer(v.Count > 0, w) }
TransformList = Transform<0,,SEP>;
Transform <AstField> = &RC { null }
    / v:FieldIdent &(SEP / RC) { AST(state).Transform(v) }
    / v:Ident LA w:FieldIdent &(SEP / RC) { AST(state).Transform(v, w) }
    / v:Ident LA w:OpenAttrExprExp { AST(state).Transform(v, null, w) }
    / v:OpenAttrExprExp { AST(state).Transform(null, null, v) };

// expressions
OpenAttrExprExp = (OpenAttrExpression 
    / #STATE{ ParseError(state, "invalid attribute expression"); } { null });
OpenExpressionExp = OpenExpression
    / #STATE{ ParseError(state, "expression expected"); } { null };
ExpressionExp = Expression
    / #STATE{ ParseError(state, "expression expected"); } { null };

OpenAttrExpression = LookupExpression; // may have a fold
LookupExpression = OpenExpression;  // needs to do name lookup
OpenExpressionList = v:OpenExpression<0,,SEP>;
OpenExpression = Expression;  // expression may be passed as value
//OpenExpression <AstValue> = v:Expression { AST(state).Code(v) };  // expression may be passed as value
ExpressionList = v:Expression<0,,SEP>;

Expression <AstValue> = v:Primary w:BinopTail* { AST(state).Expression(v,w) } 
BinopTail <AstOpCall> = v:Binop WSC w:Primary { AST(state).OpCall(v,w) } 

Primary <AstValue> = v:Unop w:Primary { AST(state).Function(v,w) }
    / v:SimplePrimary &{ PushScope(v) } w:PostFixTail* &{ PopScope() } { AST(state).PostFix(v,w) };

PostFixTail <AstCall> = RECURSE LPexp v:OpenExpressionExp RPexp { AST(state).Recurse(v) }
    / DOT v:FieldOrFuncExp { AST(state).Dot(v) }
    / &LB v:TransformTailExp { v };

SimplePrimary = DoBlock 
    / LP v:Expression RPexp { v }
    / Table / Row
    / Fold / If
    / Function
    / v:Literal { v }
    / v:VarIdent  { AST(state).Variable(v) }
    / v:Ident #STATE{ ParseError(state, "undefined '{0}'", v); } { null };

If <AstValue> = IF LPexp v:Expression SEPexp w:OpenExpression SEPexp x:OpenExpression RPexp { AST(state).If(v,w,x) }
    / IF LP  #STATE{ ParseError(state, "expression expected"); } { null };

Fold <AstValue> = FOLD LPexp v:Foldableop SEPexp w:OpenExpression RPexp { AST(state).Fold(v,w) }
    / FOLD LP  #STATE{ ParseError(state, "fold operator expected"); } { null };

DoBlock <AstValue> = DO v:DoBody { v }

Function <AstValue> = v:Funcop LP w:ExpressionList RPexp { AST(state).Function(v, w.ToArray()) };

Table <AstValue> = (TableBody 
    / LC LC #STATE{ ParseError(state, "invalid table body"); } { null } );
TableBody <AstValue> = LC LC STAR RCexp RCexp  { AST(state).Table(true) }
    / LC &LC v:TableHeading w:RowValueList RCexp { AST(state).Table(v,w) }
    / LC &LC v:RowList RCexp { AST(state).Table(null, v) };
TableHeading <AstValue> = LC COLON? RC { AST(state).Heading(null) }
    / LC &(Ident !LA (COLON / SEP)) v:FieldList RCexp { AST(state).Heading(v) };
RowValueList = v:RowValue<0,,SEP>;
RowValue <AstValue> = LP v:OpenExpressionList RPexp { AST(state).RowValues(v) };

RowList = v:Row<0,,SEP>;
Row <AstValue> = &LC v:Transformer { AST(state).Row(v) }
    / LC #STATE{ ParseError(state, "invalid attribute list"); } RCexp { null };

FieldList = v:FieldTerm<0,,SEP>;
FieldTerm <AstField> = v:Definable COLON w:Type { AST(state).Field(v, w) }
    / v:Definable { AST(state).Field(v, null) };

DefinableExp = Definable 
    / #STATE{ ParseError(state, "aleady defined"); } { null }
FieldOrFuncExp = FieldIdent / Funcop
    / #STATE{ ParseError(state, "field or function expected"); } { null }

Typename = v:Ident &{ IsTypename(v) } { v };
Foldableop = v:Funcop &{ IsFoldableop(v) } { v };
Binop = v:Funcop &{ IsBinop(v) } { v };
Unop = v:Funcop &{ IsUnop(v) } { v };
Funcop = v:(Ident / Operator) &{ IsFuncop(v) } { v };
FieldIdent = v:Ident &{ IsField(v) } { v };
VarIdent = v:Ident &{ IsVariable(v) } { v };
CatVar = v:Ident &{ IsCatVar(v) } { v };
Definable = v:Ident &{ IsDefinable(v) } { v };

// expecteds
LCexp = (WSC LC / #STATE{ ParseError(state, "left brace expected"); } );
RCexp = (WSC RC / #STATE{ ParseError(state, "right brace expected"); } );
RBexp = (WSC RB / #STATE{ ParseError(state, "right bracket expected"); } );
LPexp = (WSC LP / #STATE{ ParseError(state, "left parenthesis expected"); } );
RPexp = (WSC RP / #STATE{ ParseError(state, "right parenthesis expected"); } );
//LAexp = (WSC LA / #STATE{ ParseError(state, ":= expected"); } );
SEPexp = (WSC SEP / #STATE{ ParseError(state, "comma expected"); } );
EOLexp = (&EOL / #STATE{ ParseError(state, "end of line expected"); } Skip );

// Terminals -- each swallows following whitespace
Ident = !KEYWORD v:IdentName WSO { v };
IdentName = v:IdentQuoted WSO w:TextString { v + w }
    / IdentQuoted 
    / IdentString;
Operator = v:OpSym WSO { v };
Literal<AstValue> = v:Literals WSO { v };
Literals<AstValue> = TRUE { AST(state).Bool("true") }
    / FALSE { AST(state).Bool("false") }
    / v:BinaryQuoted { AST(state).Binary(v) }
    / v:TimeQuoted { AST(state).Time(v) }
    / v:NumberString { AST(state).Number(v) }
    / v:IntegerString { AST(state).Number(v) }
    / v:TextString { AST(state).Text(v) };
Integer = IntegerString WSO;

KEYWORD = DO / DEF / DB / TRUE / FALSE;

// note: tokens followed by WSC will swallow an EOL
DB = "db" IDX;
DEF = "def" IDX;
DO = "do" IDX;

TRUE = "true" IDX;
FALSE = "false" IDX;
RECURSE = "recurse" IDX;
IF = "if" IDX;
FOLD = "fold" IDX;

//NOT = "not" IDX;
RA = "=>" WSO;
LA = ":=" WSO;
COLON = ':' !OpChar WSO;
SEP = ',' WSC;
DOT = '.' WSO;
LC = '{' WSO;
RC = '}' WSO;   // note: gets its own handling
LP = '(' WSC;
RP = ')' WSO;
LB = '[' WSO;
RB = ']' WSO;
STAR = '*' !OpChar WSO;
QUERY = '?' WSO;
DOLLAR = '$' WSO;
PERCENT = '%' WSO;
MINUS = '-' !OpChar WSO;

IDX = !ID WS*;              // end of ident possibly followed by WS

Init <AstNode> = #STATE{ PrintLine(state); } { AST(state).Node<AstNode>(null) }
Skip = (!EOL .)* WSE;       // skip the rest of the line and hope it then makes sense
WSC = WSO (EOLX WSO)?;      // continuation, take just one eol if found, plus WS
WSE = WSO EOLX ( Directive EOLexp EOLX)*;   // end of line, take it and any following directives
WSO = WS*;                  // optional whitespace between tokens
WS = (SP / Comment) { "" };
SP = [ \t];
Comment = "//" (!EOL .)*
        / "/*" (!"*/" (EOLX / .))* "*/";

EOLX = EOL #STATE{ PrintLine(state); }; // consumed EOL, so print it
EOL = "\r"? "\n" { "" };
EOF = !.;

// More complex lexical tokens, but not for use as terminals
//TextString = v:(SingleQuoted / DoubleQuoted / CharHexQuoted / CharDecQuoted)+ { String.Concat(v) };

TextString = v:TextQuoted<1,,WSO> { String.Concat(v) };
TextQuoted = SingleQuoted / DoubleQuoted / CharHexQuoted / CharDecQuoted;

// Lexical tokens, not including white space
SingleQuoted = SQ v:("" (!(CC / SQ) . )* ) SQ { v };
DoubleQuoted = DQ v:("" (!(CC / DQ) . )* ) DQ { v };
CharHexQuoted = "h" SQ SP* v:HexDigits<1,,SP+> SP* SQ { NumToStr(v.ToArray(),16) };
//CharHexQuoted = "h" SQ v:("" SP* (HexDigit+ SP*)*) SQ { v };
CharDecQuoted = "d" SQ SP* v:Digits<1,,SP+> SP* SQ { NumToStr(v.ToArray(),10) };
BinaryQuoted  = "b" SQ v:("" HexDigit*) SQ { v };
TimeQuoted    = 't' SQ v:("" (Digit / Letter / TimeChar)+) SQ { v };
NumberString  = '.' Digit+ / Digit+ ('.' Digit*)?;
IntegerString = "" Digit+ / "$" Digit HexDigit*;
IdentQuoted   = "i" SQ v:("" (!CC !SQ .)* ) SQ { v };
IdentString = (Letter / IdChar) (Letter / Digit / IdChar / IdCharX)*;
OpSym = OpChar OpChar?;
ID = Letter / Digit / IdChar / IdCharX;
Digits = v:Digit+ { String.Concat(v) };
HexDigits = v:HexDigit+ { String.Concat(v) };

// character sets (not tokens)
Letter = [a-zA-Z];
Digit = [0-9];
HexDigit = [a-fA-F0-9];
OpChar = [&=<>:~*/+-];
IdChar = [_$@^];
IdCharX = [#%&?!~`];
TimeChar = [: ./-];
CC = [\x00-\x1f];
SQ = "'";
DQ = '"';
