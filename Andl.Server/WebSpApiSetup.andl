// WebSpApiSetup.andl
// 
#catalog new update

'WebSpApiSetup.andl -- set up for Web API SP sample'

// load initial data
var S(csv), P(csv), SP(csv)

// Assume these will be accessed directly
supplier := S .select{ *Sid:=S# }
part := P .select{ *Pid := P# }
supplies := SP .select{ *Sid := S#, Pid:= P# }

// CRUD interface for supplier table

find_supplier(sid:text) => do {
    supplier .where(Sid = sid)
}

findall_supplier() => do {
    supplier .order(Sid)
}

create_supplier(s:supplier) => do {
    update supplier union s
}

delete_supplier(sid:text) => do {
    update supplier .where(Sid = sid) .select{}
}

update_supplier(sid:text, s:supplier) => do {
    update supplier .where(Sid = sid) .select{}
    update supplier union s
}

//--- part

findall_part() => part

find_part_by_name(name:text) => do {
    part.where(PNAME =~ name)
}

tquery := {{ Key:text,Value:text }}
findsome_part(query:tquery) => do {
    pattern := query .where(Key = 'PNAME') .select{ (Value) }
    part .where(PNAME =~ pattern)
}

findall_supplies => supplies

andl_catalog
//andl_operator()
